apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = project.url

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/paulschwarz/spring-dotenv/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'paulschwarz'
                        name = 'Paul Schwarz'
                        email = 'paul@paulschwarz.me'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/paulschwarz/spring-dotenv.git'
                    developerConnection = 'scm:git:git@github.com:paulschwarz/spring-dotenv.git'
                    url = 'https://github.com/paulschwarz/spring-dotenv'
                }
            }
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            credentials {
                username prop('ossrh.username', 'OSSRH_USERNAME')
                password prop('ossrh.password', 'OSSRH_PASSWORD')
            }
            def repoBaseUrl = 'https://oss.sonatype.org'
            def releasesRepoUrl = "$repoBaseUrl/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "$repoBaseUrl/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def signingKey = prop('signing.key', 'SIGNING_KEY').replaceAll('\\\\n', '\n')
    def signingPassword = prop('signing.signingPassword', 'SIGNING_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
